plugins {
    id "com.android.library"
//    id "kotlin-android"
    id "maven-publish"
}

android {
    namespace 'com.arthenica.ffmpegkit'
    compileSdk 35
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionCode 240510
        versionName "5.1"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }
}

tasks.register('javadoc', Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

dependencies {
    implementation 'com.arthenica:smart-exception-java:0.2.1'
    testImplementation "androidx.test.ext:junit:1.2.1"
    testImplementation 'org.json:json:20220320'
}

if (System.properties.containsKey('releaseFFmpegKit')) {
    apply from: "${rootProject.projectDir}/../tools/android/release.template.gradle"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
            }
        }
    }
}
